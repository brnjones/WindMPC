# Produced by CVXGEN, 2014-05-26 18:07:40 -0400.
# CVXGEN is Copyright (C) 2006-2012 Jacob Mattingley, jem@cvxgen.com.
# The code in this file is Copyright (C) 2006-2012 Jacob Mattingley.
# CVXGEN, or solvers produced by CVXGEN, cannot be used for commercial
# applications without prior written permission from Jacob Mattingley.

# Filename: description.cvxgen.
# Description: A description of the CVXGEN problem.

dimensions
  n = 3  # states.
  T = 40  # horizon.
end

parameters
  h # timestep

  x0 (n)  # initial state.
  xf (n) #final state

  phi0    #initial phi.
  phif    #final phi
  phi_max

  u0
  uf

  a[t], t=1..T #baseline trajectory
  b[t], t=1..T
  c[t], t=1..T
  d[t], t=1..T
  e[t], t=1..T
  psi_hat[t], t=1..T
  rho[t], t=1..T
  u_max nonnegative  # amplitude limit.
end

variables
  x[t]   (n) , t=1..T  # state x, y, psi
  phi(T)
  u(T)   # input.
end

minimize
  norm_1(phi)
subject to
  x[t+1][1] == x[t][1] + a[t]*x[t][3] + b[t] , t=1..T-1
  x[t+1][2] == x[t][2] + c[t]*x[t][3] + d[t] , t=1..T-1
  x[t+1][3] == x[t][3] + e[t]* phi[t]        , t=1..T-1
  phi[t+1]  == phi[t]  + h*u[t]              , t=1..T-1

  x[1] == x0
  x[T] == xf

  phi[1] == phi0
  phi[T] == phif

  u[1] == u0
  u[T] == uf

  abs(u) <= u_max  # maximum input box constraint.
  abs(phi) <= phi_max

  abs(x[t][3] - psi_hat[t]) <= rho[t], t=1..T
end
